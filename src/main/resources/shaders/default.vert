#version 330 core

layout(location = 0) in vec3 aPos;
layout(location = 1) in vec3 aNormal;
layout(location = 2) in vec2 aTexCoord;

out vec2 vTexCoord;
out vec3 vNormal;
out vec3 vFragPos;
out vec4 vLightSpacePosition;
out float vIsWater;

uniform mat4 uProjection;
uniform mat4 uView;
uniform mat4 uModel;
uniform mat4 uLightSpaceMatrix;
uniform float uTime;

uniform vec4 uUVBounds;
uniform bool uIsWater;

void main() {
    vec3 animatedPos = aPos;
    if (uIsWater && aNormal.y > 0.5) {
        float wave = sin((aPos.x + uTime) * 4.0) * 0.015 +
        cos((aPos.z + uTime * 0.8) * 4.0) * 0.015;
        animatedPos.y += wave;
    }

    vec4 worldPosition = uModel * vec4(animatedPos, 1.0);
    gl_Position = uProjection * uView * worldPosition;

    vFragPos = worldPosition.xyz;
    vNormal = normalize(mat3(transpose(inverse(uModel))) * aNormal);
    vLightSpacePosition = uLightSpaceMatrix * worldPosition;

    vTexCoord = mix(uUVBounds.xy, uUVBounds.zw, aTexCoord);
    vIsWater = uIsWater ? 1.0 : 0.0;
}